// Project name is read-only in build scripts, and defaults to directory name.
rootProject.name = "jspecify-reference-checker"
include 'conformance-test-framework'

// Lets the main build depend on the conformance subproject as org.jspecify:conformance-test-framework.
// See https://docs.gradle.org/current/userguide/composite_builds.html#included_build_declaring_substitutions
includeBuild(".")

providers.exec {
    executable './initialize-project'
}

dependencyResolutionManagement {
    versionCatalogs {
        libs {
            version("checkerFramework", "3.42.0-eisop4")

            library("checkerFramework-checker", "io.github.eisop", "checker").versionRef("checkerFramework")
            library("checkerFramework-checker-qual", "io.github.eisop", "checker-qual").versionRef("checkerFramework")
            library("checkerFramework-framework", "io.github.eisop", "framework").versionRef("checkerFramework")
            library("checkerFramework-framework-test", "io.github.eisop", "framework-test").versionRef("checkerFramework")
            library("checkerFramework-javacutil", "io.github.eisop", "javacutil").versionRef("checkerFramework")
            library("errorProne-core", "com.google.errorprone:error_prone_core:2.18.0")
            library("errorProne-javac", "com.google.errorprone:javac:9+181-r4173-1")
            library("guava", "com.google.guava:guava:31.1-jre")

            // Related JSpecify project versions are specified in gradle.properties, and can be overridden on the Gradle command line.
            library("jspecify", libraryVersion('org.jspecify:jspecify'))
            library("jspecify-conformanceTestFramework", libraryVersion('org.jspecify.conformance:conformance-test-framework'))
            library("jspecify-conformanceTests", libraryVersion('org.jspecify.conformance:conformance-tests'))

            library("jsr305", "com.google.code.findbugs:jsr305:3.0.2")
            library("junit", "junit:junit:4.12")
            library("truth", "com.google.truth:truth:1.1.3")
        }
    }
}

String libraryVersion(String groupArtifact) {
    return "$groupArtifact:${settings[groupArtifact]}"
}
